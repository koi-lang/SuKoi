buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.antlr:antlr4-runtime:4.7.1"
    }
}

apply plugin: 'antlr'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.antlr:antlr4-runtime:4.7.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    antlr "org.antlr:antlr4:4.7.1"
}

sourceSets {
    main {
        kotlin {
            srcDirs += "src/main/kotlin"
        }
    }

    generated {
        java.srcDir "target/generated-sources/antlr4"
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-package", "org.koilang.sukoi"]
    outputDirectory = new File("target/generated-sources/antlr4".toString())
}

compileGeneratedJava {
    dependsOn generateGrammarSource
    source = sourceSets.generated.java
    source += sourceSets.main.java
}

idea {
    module {
        sourceDirs += file("target/generated-sources/antlr4/main")
    }
}

compileKotlin {
    dependsOn compileGeneratedJava

    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileKotlin.dependsOn.remove("compileJava")